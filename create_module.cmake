function(create_module)
    cmake_parse_arguments("" "STATIC;SHARED" "" "SOURCE;DEPEND;" ${ARGN})

    get_filename_component(module_name "${CMAKE_CURRENT_SOURCE_DIR}" NAME)

    if(module_name MATCHES "[^A-Za-z0-9_]")
        message(FATAL_ERROR "Module folder name can only contain [A-Za-z0-9_]")
    endif()

    if(_STATIC AND _SHARED)
        message(FATAL_ERROR "In ${module_name} module both STATIC and SHARED options given")
    elseif(_STATIC)
        set(module_linking "STATIC")
    elseif(_SHARED)
        set(module_linking "SHARED")
    else()
        set(module_linking "")
    endif()

    if(_UNPARSED_ARGUMENTS)
        message(WARNING "In ${module_name} module unrecognized argument given: ${_UNPARSED_ARGUMENTS}")
    endif()

    if(_KEYWORDS_MISSING_VALUES)
        message(WARNING "In ${module_name} module ${_KEYWORDS_MISSING_VALUES} given with no value")
    endif()

    set(module_include "${CMAKE_CURRENT_SOURCE_DIR}/include")
    set(module_interface "${CMAKE_CURRENT_BINARY_DIR}/module_interface")
    set(module_src "${CMAKE_CURRENT_SOURCE_DIR}/src")
    set(module_tests "${CMAKE_CURRENT_SOURCE_DIR}/tests")
    set(module_depend ${_DEPEND})

    if(IS_DIRECTORY "${module_include}")
        file(MAKE_DIRECTORY "${module_interface}")
        execute_process(COMMAND "${CMAKE_COMMAND}" -E create_symlink "${module_include}" "${module_interface}/${module_name}")
    endif()

    if(_SOURCE)
        if(IS_DIRECTORY "${module_include}")
            if(IS_DIRECTORY "${module_src}")
                list(TRANSFORM _SOURCE PREPEND "${module_src}/")

                add_library(${module_name} ${module_linking} ${_SOURCE})
                target_include_directories(${module_name} INTERFACE "${module_interface}" PRIVATE "${module_include}" "${module_src}")
                target_link_libraries(${module_name} PRIVATE ${module_depend})
            else()
                message(FATAL_ERROR "Source given in ${module_name} module but there's no /src directory")
            endif()
        else()
            message(FATAL_ERROR "Source given in ${module_name} module but there's no /include directory")
        endif()
    else()
        add_library(${module_name} INTERFACE)

        if(IS_DIRECTORY "${module_include}")
            target_include_directories(${module_name} INTERFACE "${module_interface}")
        endif()
    endif()

    if(BUILD_TESTING AND IS_DIRECTORY "${module_tests}")
        add_subdirectory(tests)
    endif()
endfunction()
